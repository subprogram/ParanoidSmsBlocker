buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.2'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
}

android {
    compileSdkVersion 22
    buildToolsVersion '21.1.2'

    defaultConfig {
        applicationId "ru.subprogram.paranoidsmsblocker"
        minSdkVersion 8
        targetSdkVersion 22
        versionCode 6
        versionName "1.5 beta"
    }

    def debugStorePath;
    def releaseStorePath;
    def releaseStorePassword;
    def releaseKeyAlias;
    def releaseKeyPassword;

    Properties props = new Properties()
    def propFile = project.rootProject.file('local.properties');
    if(propFile.exists()) {
        props.load(project.rootProject.file('local.properties').newDataInputStream())
        debugStorePath       = props['debugStorePath'];
        releaseStorePath     = props['releaseStorePath']
        releaseStorePassword = props['storePassword']
        releaseKeyAlias      = props['keyAlias']
        releaseKeyPassword   = props['keyPassword']
    }

    signingConfigs {
        debug {
            if(debugStorePath!=null)
                storeFile file(debugStorePath)
        }

        marketConfig {
            if(releaseStorePath!=null)
                storeFile file(releaseStorePath)
            if(releaseStorePassword!=null)
                storePassword releaseStorePassword
            if(releaseKeyAlias!=null)
                keyAlias releaseKeyAlias
            if(releaseKeyPassword!=null)
                keyPassword releaseKeyPassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.debug
        }

        release {
            minifyEnabled false
            debuggable false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
            signingConfig signingConfigs.marketConfig
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.1'
}

